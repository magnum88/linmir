#copy this file to \_ISO\MAINMENU\$$$$CONFIG folder
#create new \_ISO\GUEST folder
#see http://rmprepusb.blogspot.co.uk/2016/03/make-guest-mode-menu.html for more details (set pwd in MyE2B.cfg file)

set ASK=Y
if exist DONEPWD set ASK=
### show wallpaper
if not exist DONEPWD call Fn.70 3
clear
echo
echo !BAT > (md)0x300+1
echo -e password %pwd% \&\& set ASK= >> (md)0x300+1
if not exist DONEPWD call (md)0x300+1
if exist DONEPWD set ASK=
echo
### stop any previous animation from playing
if /i "%ASK%"=="Y" splashimage --animated=0
# next line is optional
if /i "%ASK%"=="Y" graphicsmode -1 800 > nul
### next line is optional
if /i "%ASK%"=="Y" splashimage /_ISO/docs/Templates/Jolene/Jolene800.bmp.gz > nul
### load MAINMENU wallpaper (optional)
if /i not "%ASK%"=="Y" splashimage /_ISO/e2b/grub/background.bmp.gz > nul
clear
### prevent Guest from loading Main menu
#if /i "%ASK%"=="Y" set NOF8B=1
### set up sub-folder name
if /i "%ASK%"=="Y" set MFOLDER=/_ISO/GUEST
#HDG is the top heading for the new menu
if /i "%ASK%"=="Y" set HDG=GUEST MENU
if /i "%ASK%"=="Y" (bd)/%grub%/SubMenu.g4b
set DONEPWD=1
set ASK=