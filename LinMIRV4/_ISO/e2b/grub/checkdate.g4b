!BAT
::
::             CHECKDATE.G4B
::
:: Author: Steve Si   www.rmprepusb.com
::
:: This grub4dos batch file will check to see if the CD or USB stick it has booted from has expired.
:: Place this batch file anywhere and call it from a menu.lst file (use an explicit path)
::
:: Usage: checkdate.g4b <Release Year> <Release Month> <Release DayOfMonth> <ExpiresInDays>  [<SILENT>]
::
:: User must specify Release_Year Release_Month Release_DayOfMonth as the Release Date of CD/USB stick software
:: User must set ExpiresInDays to number of days this CD/USB stick will be good for
::
:: If current date is later than (Release Date+ExpiresInDays) then user will be warned (unless SILENT is specified)
::
::
:: Examples:
::
::        checkdate.g4b 2013 01 10 30
::
::     means that the s/w was released in 2013-01-10 and 30 days are allowed before it expires
::
::        checkdate.g4b 2013 01 10 30 SILENT
::
::     as above but no messages will be produced by this batch file - only the CHECKDATE variable will be set
::
:: Returns either CHECKDATE=OK  if within expiry date or  CHECKDATE=BAD if the expiry date has been exceeded
::
:: Use in a grub4dos menu like this:
::
:: #  Usage: checkdate.g4b <Release Year> <Release Month> <Release DayOfMonth> <ExpiresInDays>  [<SILENT>]
:: /checkdate.g4b 2012 11 27 30 SILENT
:: if NOT "%CHECKDATE%"=="OK" echo THIS SOFTWARE HAS EXPIRED! && pause && reboot
::
::  or
::
:: #  Usage: checkdate.g4b <Release Year> <Release Month> <Release DayOfMonth> <ExpiresInDays>  [<SILENT>]
:: /myfolder/checkdate.g4b 2012 11 27 30
:: pause
:: if NOT "%CHECKDATE%"=="OK" reboot



# we don't want any funny messages...
debug off

if "%1"=="" echo CheckDate: ERROR: No Parameters Specified when calling %0! && pause
if "%2"=="" echo CheckDate: ERROR: Release Month Missing! && pause
if "%3"=="" echo CheckDate: ERROR: Release Day Missing! && pause
if "%4"=="" echo CheckDate: ERROR: Expiry Length Missing! && pause

set /a EYear=%1+0
set /a EMonth=%2+0
set /a EDay=%3+0
set /a ExpiresIn=%4+0


#set expiry date
#set EDay=30
#set EMonth=11
#set EYear=2012
#set number of days allowed
#set ExpiresIn=30

# Now work it out!
set d=%@date%
# date is sometimes unreliable - so retry a few times if bad!
if "%d%"=="0000-00-00" set d=%@date%
if "%d%"=="0000-00-00" set d=%@date%
if "%d%"=="0000-00-00" set d=%@date%
set CDay=%d:~8,2%
set CMonth=%d:~5,2%
set CYear=%d:~0,4%

set /a cm=%CMonth% * 31
set /a cy=%CYear% * 365
set /a cd=%CDay% + %cm% + %cy%
#echo cd=%cd%
set /a em=%EMonth% * 31
set /a ey=%EYear% * 365
set /a ed=%EDay% + %em% + %ey%
#echo ed=%ed%
# 30 days hath 9, 4, 6 11  - feb (2) has 28 days
if %EMonth%>=12 set /a ed=%ed%-1
if %EMonth%>=10 set /a ed=%ed%-1
if %EMonth%>=7 set /a ed=%ed%-1
if %EMonth%>=5 set /a ed=%ed%-1
if %EMonth%>=3 set /a ed=%ed%-3
# account for leap years
set /a lp=%EYear%\4*4
if %lp%==%EYear% set /a ed=%ed%+1

#echo cd=%cd%
# 30 days hath 9, 4, 6 11  - feb (2) has 28 days
if %CMonth%>=12 set /a cd=%cd%-1
if %CMonth%>=10 set /a cd=%cd%-1
if %CMonth%>=7 set /a cd=%cd%-1
if %CMonth%>=5 set /a cd=%cd%-1
if %CMonth%>=3 set /a cd=%cd%-3
# account for leap years
set /a lp=%CYear%\4*4
if %lp%==%CYear% set /a cd=%cd%+1
#echo cd=%cd% ed=%ed%
# add Expiry days
set /a ed=%ed% + %ExpiresIn%
set /a togo=%ed% - %cd%
if NOT "%5"=="SILENT" echo CheckDate: Release Date: %EYear%-%EMonth%-%EDay%      (Today is %d%)
if NOT "%5"=="SILENT" echo CheckDate: This version is good for %ExpiresIn% days from release
if NOT "%5"=="SILENT" if NOT "%DEBUG%"=="" echo CheckDate: Current days=%cd%  v.  Expiry days=%ed%
if %cd%>=%ed% if NOT "%5"=="SILENT" echo CheckDate: WARNING: This is older than %ExpiresIn% days! 
if NOT %cd%>=%ed% if NOT "%5"=="SILENT" echo CheckDate: This version expires in %togo% day(s)! 
if %cd%>=%ed% set CHECKDATE=BAD
if NOT %cd%>=%ed% set CHECKDATE=OK
# tidy up
set d=
set CDay=
set CYear=
set CMonth=
set EDay=
set EMonth=
set EYear=
set cd=
set ed=
set ExpiresIn=
set togo=
set cm=
set cy=
set em=
set ey=
set lp=

debug on